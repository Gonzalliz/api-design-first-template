plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "org.openapi.generator" version "5.3.0"
}

group = 'com.demo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

openApiGenerate {
	generatorName = "spring"
	validateSpec = true
	inputSpec = "$rootDir/specs/customer/customer-api.yml".toString()
	outputDir = "$rootDir/src/main/java".toString()
	apiPackage = "com.demo.bootstrap.controller"
	modelPackage = "com.demo.bootstrap.dto"
	ignoreFileOverride = "$rootDir/.openapi-generator-ignore"
	packageName = "com.demo.bootstrap"
	configOptions = [
			dateLibrary: "java8", //https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/spring.md
			interfaceOnly: "false",
			sourceFolder: "",
			basePackage: "com.demo.bootstrap",
			invokerPackage: "com.demo.bootstrap",
			openApiNullable: "false",
			skipDefaultInterface: "false"
	]
	globalProperties = [
			apiDocs: "false",
			modelDocs: "false"
	]
}

repositories {
	mavenCentral()
}

dependencies {
	// core dependencies
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// openapi-generator associated dependencies START -------------------------
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
	implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
	// https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.4'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.3.0-rc1'
	// https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
	implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.1'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
	// https://mvnrepository.com/artifact/org.springframework.data/spring-data-commons
	implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.5.4'
	// https://mvnrepository.com/artifact/javax.validation/validation-api
	implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

	// To suppress warnings in swagger-core https://github.com/springfox/springfox/issues/2265#issuecomment-424956678
	implementation("io.springfox:springfox-swagger2:2.9.2") {
		exclude module: 'swagger-annotations'
		exclude module: 'swagger-models'
	}
	implementation("io.swagger:swagger-annotations:1.5.21")
	implementation("io.swagger:swagger-models:1.5.21")

	// openapi-generator associated dependencies END -------------------------

	// test dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}
